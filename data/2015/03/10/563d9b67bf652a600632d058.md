---
id: 563d9b67bf652a600632d058
title: gulpでSVGをWebフォント化する
create: '2015-03-10 18:00'
modify: '2017-03-01 08:57'
categories:
  - Node.js
  - gulp
  - io.js
image: >-
  https://raw.githubusercontent.com/YuG1224/gulp-task-svg-webify/master/doc/sample.png
---

[YuG1224/gulp-task-svg-webify](https://github.com/YuG1224/gulp-task-svg-webify)

Web フォント化するために[IcoMoon](https://icomoon.io/)に SVG をアップロードするのが面倒なので、SVG 画像を minify して、Web フォント化する gulp タスクを作りました。

参考にしたのはここ。

<blockquote class="embedly-card" data-card-key="efc9713d77434ae8b88ef22dda0a91e8" data-card-controls="0" data-card-type="article" data-card-align="left"><h4><a href="http://whiskers.nukos.kitchen/2014/12/23/gulp-consolidate.html">[Gulp][Sketch 3][SketchTool]これからはじめるGulp（23）：gulp-consolidateでgulp-iconfontで作ったアイコンフォントのシンボル一覧HTMLを作る</a></h4><p>UI/UX Design、フロントエンド系について学習した内容をメモしています。</p></blockquote>

<!-- more -->

## 構造

全体の構造は以下。

`src/svg/`以下に SVG 画像を置けば、`dist`以下に生成される仕組みになっています。

```bash
.
├── README.md
├── dist
│   ├── css
│   ├── fonts
│   ├── index.html
│   └── svg
├── doc
├── gulpfile.coffee
├── node_modules
├── package.json
└── src
    ├── svg
    ├── template.css
    └── template.html
```

## gulp minify

svg の minify には`gulp-svgmin`を使います。

`./src/svg/`以下に置いた SVG を圧縮して、`./dist/svg/`以下に出力します。

```coffee
gulp = require "gulp"
foreach = require "gulp-foreach"
svgmin = require "gulp-svgmin"
concat = require "gulp-concat"

# minify
gulp.task "minify", () ->
  return gulp.src ["src/svg/*.svg"]
    .pipe foreach (stream, file) ->
      filename = file.path.replace file.base, ""
      stream.pipe svgmin()
        .pipe concat filename
    .pipe gulp.dest "dist/svg"
```

## gulp webify

minify した SVG を元に`gulp-iconfont`と`gulp-consolidate`で webfont 化します。

template 用の css と html は[cognitom/symbols-for-sketch](https://github.com/cognitom/symbols-for-sketch/tree/master/templates)にある template を自分が好きな Handlebars 形式に改変して使わせて頂きました。

Web フォント化した時にデフォルトでは Unicode の私用領域の最初 E001 から埋めていくのですが、SoftBank の絵文字と衝突してしまい環境によっては正しく表示されなくなります。

<blockquote class="embedly-card" data-card-key="efc9713d77434ae8b88ef22dda0a91e8" data-card-controls="0" data-card-type="article-full" data-card-align="left"><h4><a href="https://ja.wikipedia.org/wiki/%E6%90%BA%E5%B8%AF%E9%9B%BB%E8%A9%B1%E3%81%AE%E7%B5%B5%E6%96%87%E5%AD%97">携帯電話の絵文字 - Wikipedia</a></h4><p>文字コードは、多くの事業者で Shift_JISもしくは Unicodeの空き領域に2バイトのコードを割り当てて使用している（従って 外字の一種）。ただし SoftBank（Yahoo!ケータイ）ではShift_JISの場合に特殊な エスケープシーケンスを用いて文字を表記する方式であり、 au (EZWeb) では HTMLの タグを独自に拡張した表記法もある。このほか、HTMLの数値文字参照の形で文字コードを十進数もしくは十六進数で記述する方法もあるが、機種によって絵文字が表示できない場合もある。当初はISベースのコードのみでの対応であったが、その後 Unicode の外字にマッピングした絵文字も利用されるようになった。</p></blockquote>

スマホ利用もあると考えて、`options.startCodepoint`を`0xF001`にしてみました。

```coffee
gulp = require "gulp"
foreach = require "gulp-foreach"
iconfont = require "gulp-iconfont"
consolidate = require "gulp-consolidate"
concat = require "gulp-concat"
rs = require("run-sequence").use(gulp)

options =
  fontName: "webfont"
  className: "wf"
  startCodepoint: 0xF001

# webify
gulp.task "webify", () ->
  return gulp.src ["dist/svg/*.svg"]
    .pipe iconfont options
    .on "codepoints", (codepoints, options) ->
      for val in codepoints
        val.codepoint = val.codepoint.toString(16).toUpperCase()
      engine = "handlebars"
      consolidateOptions =
        glyphs: codepoints,
        fontName: options.fontName
        fontPath: "../fonts/"
        className: options.className

      gulp.src "src/template.css"
        .pipe consolidate engine, consolidateOptions
        .pipe concat "#{options.fontName}.css"
        .pipe gulp.dest "dist/css/"

      gulp.src "src/template.html"
        .pipe consolidate engine, consolidateOptions
        .pipe concat "index.html"
        .pipe gulp.dest "dist/"

    .pipe gulp.dest "dist/fonts/"
```

## gulp webserver

SVG を編集しながら、生成された Web フォントをブラウザで確認するために、webserver のタスクも作りました。

`src/svg/*.svg`の変更を監視して minify,webify が実行され、`dist`の変更を監視して livereload 機能が動きます。

![debug画面](https://raw.githubusercontent.com/YuG1224/gulp-task-svg-webify/master/doc/sample.png)

```coffee
gulp = require "gulp"
del = require "del"
rs = require("run-sequence").use(gulp)
webserver = require "gulp-webserver"

# watch
gulp.task "watch", () ->
  gulp.watch ["src/svg/*.svg"], () ->
    rs "clean", "minify", "webify"

# webserver
gulp.task "webserver", ["compile", "watch"], () ->
  gulp.src "dist"
    .pipe webserver
      livereload: true

# clean
gulp.task "clean", (done) ->
  del ["dist/*/*"], done

# initialize
gulp.task "initialize", (done) ->
  del ["dist"], done

# compile
gulp.task "compile", (done) ->
  rs "initialize", "minify", "webify", done
```

## まとめ

```bash
$ gulp compile    // minify, webify を1回のみ実行
$ gulp webserver  // 変更を監視して、minify, webify を実行し、ブラウザ画面を自動更新
```

途中、`gulp-iconfont`が最低 500x500 以上の SVG が対象ということに気づかず、Web フォントが崩れてしまう現象にハマった。

素材が 500x500 以上というのが気になるけれど、ひとまずこれで IcoMoon にアップしなくてもローカル環境で Web フォント化することが出来るようになった。
